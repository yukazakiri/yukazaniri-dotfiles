import qs.modules.common
import qs.modules.common.widgets
import QtQuick
import QtQuick.Controls
import QtQuick.Layouts

ToolTip {
    id: root
    property bool extraVisibleCondition: true
    property bool alternativeVisibleCondition: false

    // Visibility logic:
    // - If parent has buttonHovered (RippleButton), use it
    // - Else if parent has hovered, use it  
    // - Else default to true (for components without hover tracking)
    readonly property bool parentHoverState: {
        if (parent.buttonHovered !== undefined) return parent.buttonHovered
        if (parent.hovered !== undefined) return parent.hovered
        return true  // Default: show tooltip if no hover property exists
    }
    readonly property bool internalVisibleCondition: (extraVisibleCondition && parentHoverState) || alternativeVisibleCondition
    verticalPadding: 5
    horizontalPadding: 10
    background: null
    font {
        family: Appearance.font.family.main
        variableAxes: Appearance.font.variableAxes.main
        pixelSize: Appearance?.font.pixelSize.smaller ?? 14
        hintingPreference: Font.PreferNoHinting // Prevent shaky text
    }

    visible: internalVisibleCondition
    
    contentItem: StyledToolTipContent {
        id: contentItem
        font: root.font
        text: root.text
        shown: root.internalVisibleCondition
        horizontalPadding: root.horizontalPadding
        verticalPadding: root.verticalPadding
    }
}
